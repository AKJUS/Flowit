apply plugin: 'com.android.application'
apply plugin: 'com.github.triplet.play'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"

    playAccountConfigs {
        defaultAccountConfig {
            if (project.hasProperty("TRIPLET_API_KEY")) {
                jsonFile = file(TRIPLET_API_KEY)
            }
        }
    }

    play {
        track = 'alpha'
        uploadImages = true
    }

    defaultConfig {
        applicationId "com.bytehamster.flowitgame"
        minSdkVersion 14
        targetSdkVersion 26

        playAccountConfig = playAccountConfigs.defaultAccountConfig
    }

    signingConfigs {
        release {
            if (project.hasProperty("SIGNING2")) {
                storeFile file(SIGNING2_STORE_FILE)
                storePassword SIGNING2_STORE_PASSWORD
                keyAlias SIGNING2_KEY_ALIAS
                keyPassword SIGNING2_KEY_PASSWORD
            }
        }
    }

    buildTypes {
        debug {
            buildConfigField "boolean", "DEBUG_LEVELS", "false"
            signingConfig signingConfigs.debug
        }
        debugLevels {
            buildConfigField "boolean", "DEBUG_LEVELS", "true"
            signingConfig signingConfigs.debug
        }
        release {
            buildConfigField "boolean", "DEBUG_LEVELS", "false"
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
        }
    }

    task compressLevelFile << {
        String levels = file(projectDir.path + '/src/main/assets/levels.xml').text
        print '  Original: ' + levels.length() + ' bytes'
        levels = levels.replaceAll("\\s+", " ").replaceAll("\"\n ?", "\" ").replaceAll(" ?\n ?", "")
        levels = levels.replaceAll("=\" ", "=\"").replaceAll("<!--([^>]*)-->", "") + '\n'
        println ', compressed: ' + levels.length() + ' bytes'
        file(projectDir.path + '/src/main/assets/levels_compressed.xml').text = levels
    }
    preBuild.dependsOn compressLevelFile
}

dependencies {
    compile 'com.google.android.gms:play-services-ads-lite:11.0.4'
}

apply plugin: 'com.google.gms.google-services'
